plugins {
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'com.android.application'
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    if (System.getenv("ANDROID_NDK_HOME") != null) {
        ndkPath "$System.env.ANDROID_NDK_HOME"
    }
    compileSdk 34

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }


    defaultConfig {
        applicationId "io.rebble.cobble"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        android.defaultConfig.vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {
            keyAlias = "upload"
            keyPassword = "$System.env.ALIAS_PASSWORD"
            storeFile file("../key.jks")
            storePassword = "$System.env.KEY_PASSWORD"
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    // For Kotlin projects
    kotlinOptions {
        jvmTarget = "1.8"
    }
    namespace 'io.rebble.cobble'
    lint {
        checkReleaseBuilds false
        disable 'InvalidPackage'
    }
}
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
        freeCompilerArgs += "-opt-in=kotlin.ExperimentalUnsignedTypes"
        freeCompilerArgs += "-opt-in=kotlinx.serialization.ExperimentalSerializationApi"
    }
}

flutter {
    source '../..'
}

def libpebblecommon_version = '0.1.13'
def coroutinesVersion = "1.7.3"
def lifecycleVersion = "2.8.0"
def timberVersion = "4.7.1"
def androidxCoreVersion = '1.13.1'
def daggerVersion = '2.50'
def workManagerVersion = '2.9.0'
def okioVersion = '3.7.0'
def serializationJsonVersion = '1.3.2'

def junitVersion = '4.13.2'
def androidxTestVersion = "1.5.0"

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$serializationJsonVersion"
    implementation "io.rebble.libpebblecommon:libpebblecommon-android:$libpebblecommon_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-service:$lifecycleVersion"
    implementation "com.jakewharton.timber:timber:$timberVersion"
    implementation "androidx.core:core-ktx:$androidxCoreVersion"
    implementation "androidx.work:work-runtime-ktx:$workManagerVersion"
    implementation "com.squareup.okio:okio:$okioVersion"

    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation project(':pebble_bt_transport')
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    testImplementation "junit:junit:$junitVersion"

    androidTestImplementation "androidx.test:runner:$androidxTestVersion"
    androidTestImplementation "androidx.test:rules:$androidxTestVersion"
}

android.buildTypes.release.ndk.debugSymbolLevel = 'FULL'
